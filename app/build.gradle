apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.triplet.play'

android {
    if (project.rootProject.file('playstore.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('playstore.properties').newDataInputStream())
        play {
            serviceAccountEmail = properties.getProperty('play.serviceaccountemail')
            pk12File = file(properties.getProperty('play.pk12file'))
            uploadImages = true
        }
        signingConfigs {
            config {
                storeFile file(properties.getProperty('keystore.path'))
                storePassword properties.getProperty('keystore.password')
                keyAlias properties.getProperty('keystore.key.githubbrowser.alias')
                keyPassword properties.getProperty('keystore.key.githubbrowser.password')
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.config
            }
        }
    }
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "info.mschmitt.githubapp.f"
        minSdkVersion sdk.minVersion
        targetSdkVersion sdk.targetVersion
        versionCode 12
        versionName "1.0.12"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
    }
}

dependencies {
    compile project(':libraries:network')
    compile project(':libraries:domain')
    compile project(':libraries:utilities-android')
    compile project(':libraries:utilities-java')
    compile libraries.supportAppCompat
    compile libraries.supportDesign
    compile libraries.supportAnnotations
    compile libraries.dagger
    apt libraries.daggerCompiler
    compile libraries.rxAndroid
    compile libraries.rxJava
}
